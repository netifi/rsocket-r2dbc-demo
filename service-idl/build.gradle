plugins {
    id 'com.google.protobuf' version '0.8.6'
    id 'java'
}

sourceCompatibility = 1.8

dependencies {
    compile "io.rsocket.rpc:rsocket-rpc-core:$rsocketRpcVersion"
}

sourceSets {
    main {
        proto { srcDir 'src/main/proto' }
    }

    test {
        proto { srcDir 'src/test/proto' }
    }
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        rsocketRpc {
            artifact = "io.rsocket.rpc:rsocket-rpc-protobuf:$rsocketRpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            rsocketRpc {}
        }
    }
}

idea {
    module {
        sourceDirs += file("src/main/proto")
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/rsocketRpc")

        generatedSourceDirs += file('src/generated/main/java')
        generatedSourceDirs += file('src/generated/main/rsocketRpc')
    }
}

clean {
    delete 'src/generated'
}
