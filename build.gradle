buildscript {
  ext {
	springBootVersion = '2.1.0.RC1'
  }
  repositories {
	jcenter()
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
  }

  dependencies {
	classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
	classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
  }
}

apply from: "gradle/doc.gradle"

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'io.spring.dependency-management'

  group 'io.rsocket.springone.demo'
  version '1.0.0'

  sourceCompatibility = 1.8


  ext['protobuf.version'] = '3.6.1'
  ext['rsocket.version'] = '0.11.9'
  ext['rsocket-rpc.version'] = '0.2.4'
  ext['proteus.version'] = '1.5.0-RC'

  dependencyManagement {
	dependencies {
	  dependencySet(group: 'com.google.protobuf', version: ext['protobuf.version']) {
		entry 'protoc'
		entry 'protobuf-java'
		entry 'protobuf-java-util'
	  }
	  dependencySet(group: 'io.rsocket', version: ext['rsocket.version']) {
		entry 'rsocket-core'
		entry 'rsocket-transport-netty'
		entry 'rsocket-transport-local'
	  }
	  dependencySet(group: 'io.rsocket.rpc', version: ext['rsocket-rpc.version']) {
		entry 'rsocket-rpc-core'
		entry 'rsocket-rpc-metrics-idl'
		entry 'rsocket-rpc-protobuf'
		entry 'rsocket-rpc-protobuf-idl'
	  }
	  dependency "io.netifi.proteus:proteus-spring-boot-starter:${ext['proteus.version']}"
	}
  }

  repositories {
	jcenter()
	maven { 
	  url 'http://repo.spring.io/libs-snapshot'
	}
	maven {
	  url "https://dl.bintray.com/netifi/netifi-oss"
	}
	maven {
	  url = 'https://artifactory.netifiinc.com/artifactory/libs-snapshot-local'
	  credentials {
		username = project.property('netifiArtifactoryUsername')
		password = project.property('netifiArtifactoryPassword')
	  }
	}
	maven {
	  url = 'https://artifactory.netifiinc.com/artifactory/libs-release-local'
	  credentials {
		username = project.property('netifiArtifactoryUsername')
		password = project.property('netifiArtifactoryPassword')
	  }
	}
  }
}

configurations.all {
  resolutionStrategy {
	dependencySubstitution {
	  substitute module('com.google.guava:guava') with module('com.google.guava:guava:22.0')
	}
  }
}